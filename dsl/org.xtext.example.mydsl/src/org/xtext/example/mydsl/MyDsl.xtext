grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

Domainmodel:
    (elements+=AbstractElement)*;
    
AbstractElement :
	PackageDeclaration|Type|Import;

PackageDeclaration:
	'package' name=QualifiedName '{'
		(element+=AbstractElement)*
	'}';

QualifiedName:
	ID ('.' ID)*;

Import:
	'import' importNamespace=QualifiedNameWithWildcard;

QualifiedNameWithWildcard:
	QualifiedName '.*'?;

Type:
    DataType | Entity;
  
DataType:
    'datatype' name=ID;
 
Entity:
    'entity' name=ID ('extends' superType=[Entity|QualifiedName])? '{'
        (features+=Feature)*
    '}';
 
Feature:
    (many?='many')? name=ID ':' type=[Type|QualifiedName];